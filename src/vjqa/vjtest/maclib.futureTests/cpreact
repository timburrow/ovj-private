"cpreact - recover from error conditions during werr processing"

"no action if high noise"

write('line3','call cpreact')
acqstatus?
acqstatus[1]?
acqstatus[2]?
if (r2=1) then
  acqstatus=14
  acqstatus[2]=103
  r2=0
endif
acqstatus?
acqstatus[1]?
acqstatus[2]?

if (acqstatus[ 2 ] = 102) then
    return
endif


"In automation mode,  certain errors must cause the automation"
"run to be terminated.  Errors larger than 900 are unrecoverable"
"The 500 series of errors are sample changer errors.  All sample"
"changer errors except one will abort an automation run.  The"
"exception (error 521) occurs if the sample changer attempts to"
"take a sample from the tray but fails to find one there.  In that"
"case,  the automation run just continues"
if (auto = 'y') then
    if ((acqstatus[ 2 ] >= 900) or
        ((acqstatus[ 2 ] >= 500) and (acqstatus[ 2 ] < 600) and
         (acqstatus[ 2 ] <> 521) and (acqstatus[ 2 ] <> 575))) then
       autosa
       cpwerr
       geterror:$error
       sqLog('ExpError',$error)
       return('abort')
    endif
    return
endif

"if abort acquisition, check for special error messages	     "
"in non-automation mode (auto=n), r1 is used by previous     "
"use of react macro to generate a proper error message on    "
"abort (otherwise the abort message itself would immediately "
"erase the message of interest). in automation mode (auto=y),"
"messages are written directly into the log file"

"since the error codes on the VXR-5000 series now go up to   "
"1104, r1 receives 10000 + error code value.                 "

$ret=''
if (acqstatus[ 1 ] = 16) then
    $r1=0
    if (size('acqstatus') = 3) then
      $r1=acqstatus[3]
acqstatus[3]?
      acqstatus[3]=0
    endif
    if ($r1=10103)or($r1=10104) then           "high gain abort"
        write('line3','signal too large - reduce gain or pulse width')
    endif
    if $r1=10801 then                          "autogain abort"
        write('line3','signal too large - reduce pulse width or concentration')
    endif
    if $r1=10001 then                            "gain reduced"
        if gain>5 then
            gain=gain-6
        else
            gain=0
        endif
        au('restart')
        write('line3','gain reduced and experiment resubmitted')
    elseif $r1=10002 then                              "pw reduced"
	pw=0.5*pw
        au('restart')
        write('line3','pw reduced and experiment resubmitted')
    else
      cpwerr
      $ret='next'
    endif
    return($ret)
endif

"try to optimize lock conditions if lock lost"

if (acqstatus[ 2 ] = 201) and (acqstatus[ 1 ] = 15) then
    if alock<>'a' then
      alock='a' wshim='e'
      au('restart')
    else
      cpwerr
      $ret='next'
    endif
    return($ret)
endif

if (acqstatus[2]=301) or (acqstatus[2]=302) then
  if (acqstatus[1]<>14) then
    spin=0 alock='y' wshim='n'
    au('restart')
  endif
  return
endif

"find out if it's 1D or 2D"

getdim:$dim
if ( ($dim > 1) or (arraydim > 1) ) and
   ( (acqstatus[ 2 ] = 103) or (acqstatus[ 2 ] = 104) or (acqstatus[ 2 ] = 801) ) then
    acqstatus[3] = acqstatus[ 2 ]+10000
    aa                            "abort acquisition"
    return                        "which causes this macro to be reexecuted"
endif

if (acqstatus[ 2 ] = 103) or      "receiver or adc overload,"
   (acqstatus[ 2 ] = 104) then    "should only get this condition"
                                  "if not in autogain mode"
    if gain<>0 then
        acqstatus[3]=10001
	getvalue('ct'):ct
        if ct=0 then
	  aa			"Abort current acquisition"
        endif
        return
    else                          "gain at minimum, reduce pw"
        if pw<2 then
            acqstatus[3]=10801
            aa                    "abort acquisition"
            return
        else
	    getvalue('ct'):ct
	    acqstatus[3]=10002
	    if ct=0 then
	      aa		"Abort current acquisition"
	    endif
            return
        endif
    endif
endif

if (acqstatus[ 2 ] = 801) then    "autogain fail: gain=0 and still overloaded"
    if pw<2 then
        write('line3','signal too large - reduce concentration')
        resume
        return
    endif
    pw=0.5*pw                     "reduce pulse width and try again"
    write('line3','pulse width lowered and experiment resubmitted')
    au('restart')
    return
endif

if (acqstatus[ 2 ] = 200) then                      "max transients accumulated"
    dp='y' 
    write('line3','double precision acquisition selected; experiment resubmitted')
    au('restart')
    return
endif

cpwerr
return('next')

