"Copyright (c) Varian Assoc., Inc.  All Rights Reserved."
" ********************** "
" **** macro bootup **** "
" ********************** "
$jvp=''
format(jviewport,0,0):$jvp
jFunc(55,'/vnmr/tmp/vp'+$jvp)
debug('c2')
if (appmode<>'imaging') then
  exists('activestudy','parameter','global'):$e
  if ($e) then activestudy='null' endif
  exists('sqmode','parameter','global'):$e
  if ($e) then sqmode='pause' endif
  exists('printout','parameter','global'):$e
  if ($e) then printout='printer' endif
  exists('reqparcheck','parameter','global'):$e
  if ($e) then reqparcheck='n' endif
endif
if ($# < 0.5) then
  $bg = 0
else
  $bg = $1
  if ($bg = 0) then  " Foreground Vnmr "
    $file=systemdir+'/bootup_message'
    exists($file,'file'):$e
    if ($e<>0) then
      cat($file)
    endif 
    acct('start')
  endif

  unit('p','ppm','reffrq')
  unit('k','kHz',1000)
  unit('d','ppm','reffrq1')
  
  "loads display and plotter colors"
  loadcolors  
  
  exists('login','maclib'):$e 
  if ($e<>0) then
    login($bg)
  endif
  exists('/vnmr/acqqueue/probe','file'):$e
  if ($e > 0.5) then
     lookup('file','/vnmr/acqqueue/probe','read'):probe
  endif
  if ($bg > 0.5) then
    if (appmode = 'walkup') then
       testacquire='y'
    endif
    dousermacro('bootup')
    return
  endif
endif

isvnmrj:$j
jexp:$ex
if ($ex=0) then
  if (jcurwin[jviewport] > 0) then
    unlock(jcurwin[jviewport])
    jexp:$ex
  endif
endif
if ($ex=0) then
  write('line3','Cannot unlock workspace in viewport %d - maybe it is used by another process.',jviewport)
  create('seqfil','string')
  create('file','string')
  create('arraydim','real')
  seqfil='exp0'
endif

if (system <> 'datastation') and ($# > 0.5) then
  expactive:$isactive
  if ($isactive > -1) then
    if ($j < 0.5) then
      acqstat
      acqi('standby'):$e
    else
      locki('standby')
    endif
  endif
endif

if ($j > 0.5) and ($# > 0.5) then
  mfpars
  if (jviewport=1) then
    setvtairlimits
    $dir=userdir+'/persistence'
    exists($dir,'directory'):$e
    if ($e < 0.5) then
      mkdir($dir)
    endif
    if ($bg = 0) then
      $file=$dir+'/.talk2j'
      exists($file,'file'):$e
      if ($e < 0.5) then
        write('file',$file,'%s',vnmraddr)
        shell('chmod 666 "'+$file+'"'):$x
      endif
    endif
    sqfilemenu('init')
    showloginbox
  endif
endif

if ($ex = 0) then
  menu('off')
  dousermacro('bootup')
  return
endif

if ($j > 0.5) then
  exists('appmode','parameter','global'):$e
  if ($e > 0.5) then
    if (appmode = 'imaging') then
      sqpars
      rmg3dpars "clean out old current tree parameters"
      "g3dpars   add new viewport global parameter"
      jFunc(100,0,2)
      exists('mshowoverlap','parameter','global'):$e2
      if ($e2 < 0.5) then
	    mmpars
      endif
      exists('aipDisplayMode','parameter','global'):$e
      exists('aipLayoutPolicy','parameter','global'):$e2
      exists('showObliquePlanesPanel','parameter','global'):$e3
      exists('aipMoviePath','parameter','global'):$e4
      if ($e4 < 0.5 or $e2 < 0.5 or $e3 < 0.5 or $e < 0.5 or size('aipDisplay') < 3) then
        aippars
      endif
      vnmrjcmd('SQ ignoreInactiveVp 0')
    else
      cqpars
    endif
    if ($# > 0.5) then
      if (appmode='imaging') then
        exists('sqdir','parameter','global'):$e
        if ($e>0.5 and jviewport=1) then
          activestudy='null'
          exists(sqdir+'/study.xml','file'):$e
          if ($e > 0.5) then
            xm1('read "'+sqdir+'/study.xml"')
          endif
        endif
        exists('copyid','parameter','global'):$x
        if $x>0.5 then copyid='null','null' endif
      elseif (appmode <> 'walkup') then
        activestudy='null'
      endif
    endif
    if (appmode='') then
      appmode='walkup'
      writeparam(userdir+'/global','appmode','global','add')
    endif
    if (appmode = 'imaging') then
      bootimaging
    else
      bootwalkup
    endif
  endif
endif
menu('main')
dousermacro('bootup')
